plugins {
    id "java-library"
    id "maven-publish"
    id "com.diffplug.spotless" version "7.0.3"
    id "net.nemerosa.versioning" version "3.1.0"
}

group = "com.github.malczuuu"
version = (versioning.info.tag ?: "${versioning.info.lastTag}-${versioning.info.build}") \
            + (versioning.info.dirty ? "-dirty" : "")

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.12.2")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.12.2")

    testRuntimeOnly("org.junit.platform:junit-platform-launcher:1.12.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.12.2")
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/malczuuu/github-packages-draft"
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.token") ?: System.getenv("GPR_TOKEN")
            }
        }
    }
}

import com.diffplug.spotless.LineEnding

spotless {
    format "misc", {
        target "*.gradle", ".gitattributes", ".gitignore"

        trimTrailingWhitespace()
        leadingTabsToSpaces()
        endWithNewline()
    }

    java {
        target "src/**/*.java"

        googleJavaFormat("1.28.0")
        lineEndings = LineEnding.UNIX
    }
}

tasks.named("test") {
    useJUnitPlatform()
}
